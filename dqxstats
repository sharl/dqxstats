#!/usr/bin/env perl
# -*- coding: utf-8 -*-
use strict;
use warnings;
use File::Basename 'basename';
use Config::Tiny;
use AnyEvent;
use AnyEvent::HTTP;

my $conf = $ENV{HOME}.'/.'.basename($0);
my $format = '%sの%sのレベルが%sになった！';
my $repute = '%sの%sの評判が%sになった！';

my $ini = Config::Tiny->read($conf) || Config::Tiny->new;

if (defined $ARGV[0] && $ARGV[0] eq '-p') {
    my $infofmt =<<EOD;
%s
|戦|僧|魔|武|盗|旅|バ|パ|魔|レ|賢|ス|ま|ど|
|士|侶|使|闘|賊|芸|ト|ラ|戦|ン|者|パ|も|う|
|%s|%s|%s|%s|%s|%s|%s|%s|%s|%s|%s|%s|%s|%s|
EOD

    foreach my $id (keys %{$ini}) {
	printf $infofmt, $ini->{$id}{name}, map {
	    my $level = $ini->{$id}{$_};
	    $level ? (sprintf '%2d', $level) : '　';
	} (qw(戦士 僧侶 魔法使い 武闘家 盗賊 旅芸人
	      バトルマスター パラディン 魔法戦士 レンジャー 賢者 スーパースター まもの使い どうぐ使い));
    }
    exit;
}

my ($pat) = (
    '<h2 id="myCharacterName">\[(.*?)\]</h2>.*?'.
    '<div id="myCharacterStatusList">.*?<dl>.*?<dt>職　業</dt>.*?<dd>：&nbsp;(.*?)</dd>.*?<dt>レベル</dt>.*?<dd>：&nbsp;(\d+)</dd>'.
    '(.*?<h2 class="iconTitle">職人レベル</h2>.*?<th><span>(.*?)</span></th>.*?<td><span>Lv (\d+)</span></td>.*?'.
    '<th><span>評判</span></th>.*?<td>(.*?)</td>)?'.
    '(.*?<th><span>釣りレベル</span></th>.*?<span>Lv&nbsp;(\d+)</span>)?'
    );

my $change;
my $cv = AnyEvent->condvar;

sub request {
    my $id = shift;

    my $target = 'http://hiroba.dqx.jp/sc/character/'.$id.'/status/';

    $cv->begin;
    http_get $target, timeout => 5, recurse => 0, sub {
	my ($body, $headers) = @_;

	if ($headers->{Status} == 200) {
	    my ($name, $job, $level, undef, $skill, $slevel, $srepute, undef, $flevel) = $body =~ /$pat/so;
	    unless ($name) {
		$cv->end;
		return;
	    }

	    if (defined $ini->{$id}{name}) {
		if (! defined $ini->{$id}{$job} || $ini->{$id}{$job} ne $level) {
		    printf($format."\n", $name, $job, $level);
		    $ini->{$id}{$job} = $level;
		    ++$change;
		}
		if (defined $skill && (! defined $ini->{$id}{$skill} || $ini->{$id}{$skill} ne $slevel)) {
		    printf($format."\n", $name, $skill, $slevel);
		    $ini->{$id}{$skill} = $slevel;
		    ++$change;
		}
		if (defined $srepute && (! defined $ini->{$id}{'評判'} || $ini->{$id}{'評判'} ne $srepute)) {
		    printf($repute."\n", $name, $skill, $srepute);
		    $ini->{$id}{'評判'} = $srepute;
		    ++$change;
		}
		if (defined $flevel && (! defined $ini->{$id}{'釣り'} || $ini->{$id}{'釣り'} ne $flevel)) {
		    printf($format."\n", $name, '釣り', $flevel);
		    $ini->{$id}{'釣り'} = $flevel;
		    ++$change;
		}
	    } else {
		$ini->{$id}{name} = $name;
		$ini->{$id}{$job} = $level;
		$ini->{$id}{$skill} = $slevel  if $skill;
		$ini->{$id}{'評判'} = $srepute if $srepute;
		$ini->{$id}{'釣り'} = $flevel  if $flevel;
		++$change;
	    }
	}
	$cv->end;
    };
}

request($_) for (@ARGV ? @ARGV : keys %{$ini});
$cv->recv;
$ini->write($conf) if $change;
