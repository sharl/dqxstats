#!/usr/bin/env perl
# -*- coding: utf-8 -*-
use strict;
use warnings;
use File::Basename 'basename';
use Config::Tiny;
use AnyEvent;
use AnyEvent::HTTP;

my $conf = $ENV{HOME}.'/.'.basename($0);
my $format = '%sの%sのレベルが%sになった！';
my $repute = '%sの%sの評判が%sになった！';

my %result = ();
my $cv = AnyEvent->condvar;

sub request {
    my $id = shift;

    my $target = 'http://hiroba.dqx.jp/sc/character/'.$id.'/status/';

    $cv->begin;
    http_get $target, timeout => 5, recurse => 0, sub {
	my ($body, $headers) = @_;

	my ($pat) = (
	    '<h2 id="myCharacterName">\[(.*?)\]</h2>.*?'.
	    '<div id="myCharacterStatusList">.*?<dl>.*?<dt>職　業</dt>.*?<dd>：&nbsp;(.*?)</dd>.*?<dt>レベル</dt>.*?<dd>：&nbsp;(\d+)</dd>'.
	    '(.*?<h2 class="iconTitle">職人レベル</h2>.*?<th><span>(.*?)</span></th>.*?<td><span>Lv (\d+)</span></td>.*?'.
	    '<th><span>評判</span></th>.*?<td>(.*?)</td>)?'.
	    '(.*?<th><span>釣りレベル</span></th>.*?<span>Lv&nbsp;(\d+)</span>)?'
	    );
	if ($headers->{Status} == 200) {
	    my ($name, $job, $level, undef, $skill, $slevel, $srepute, undef, $flevel) = $body =~ m,$pat,so;
	    $result{$id} = {
		name => $name,
		job => $job,
		level => $level,
		skill => $skill,
		slevel => $slevel,
		srepute => $srepute,
		flevel => $flevel,
	    } if $name;
	}
	$cv->end;
    };
}

sub run {
    my @ids = @_;

    my $ini = Config::Tiny->read($conf) || Config::Tiny->new;
    @ids = keys %{$ini} unless @ids;

    request($_) for @ids;
    $cv->recv;

    my $change;
    for my $id (@ids) {
	next unless my $name = $result{$id}{name};
	my $job     = $result{$id}{job};
	my $level   = $result{$id}{level};
	my $skill   = $result{$id}{skill};
	my $slevel  = $result{$id}{slevel};
	my $srepute = $result{$id}{srepute};
	my $flevel  = $result{$id}{flevel};

	if (defined $ini->{$id}{name}) {
	    if ($ini->{$id}{$job} ne $level) {
		printf($format."\n", $name, $job, $level);
		$ini->{$id}{$job} = $level;
		++$change;
	    }
	    if (defined $skill && (! defined $ini->{$id}{$skill} || $ini->{$id}{$skill} ne $slevel)) {
		printf($format."\n", $name, $skill, $slevel);
		$ini->{$id}{$skill} = $slevel;
		++$change;
	    }
	    if (defined $srepute && (! defined $ini->{$id}{'評判'} || $ini->{$id}{'評判'} ne $srepute)) {
		printf($repute."\n", $name, $skill, $srepute);
		$ini->{$id}{'評判'} = $srepute;
		++$change;
	    }
	    if (defined $flevel && (! defined $ini->{$id}{'釣り'} || $ini->{$id}{'釣り'} ne $flevel)) {
		printf($format."\n", $name, '釣り', $flevel);
		$ini->{$id}{'釣り'} = $flevel;
		++$change;
	    }
	} else {
	    $ini->{$id}{name} = $name;
	    $ini->{$id}{$job} = $level;
	    $ini->{$id}{$skill} = $slevel  if $skill;
	    $ini->{$id}{'評判'} = $srepute if $srepute;
	    $ini->{$id}{'釣り'} = $flevel  if $flevel;
	    ++$change;
	}
    }
    $ini->write($conf) if $change;
}

run(@ARGV);
